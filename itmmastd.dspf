     A*----------------------------------------------------------
     A*               ITEM MASTER FILE SCREEN DESIGN
     A*
     A*   Name......: ITMMASTD
     A*   Purpose...: Subfile for Maintaining Item Master File
     A*   Notes.....: Single Page Subfile
     A*   Written By: Ravisankar Pandian
     A*   Company...: Programmers.IO
     A*   Date......: 19-07-2023
     A*
     A*----------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CF05(05 'Refresh')
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Cancel')
     A                                      PAGEUP(25)
     A                                      PAGEDOWN(26)
     A          R ITSFL                     SFL
     A*%%TS  SD  20230706  060227  RAVISANKAR  REL-V7R4M0  5770-WDS
     A                                      TEXT('Item Master Subfile')
     A  67                                  SFLNXTCHG
     A            S_RRN          4S 0H
     A            S_OPT          1A  B 10  2
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A            S_ITNUM        5S 0O 10  7
     A            S_ITDESC      30A  O 10 20
     A            S_ITQTY        5S 0O 10 53
     A            S_ITPRICE      5S 0O 10 69
     A          R ITCTL                     SFLCTL(ITSFL)
     A                                      OVERLAY
     A*%%TS  SD  20230705  085013  RAVISANKAR  REL-V7R4M0  5770-WDS
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0005)
     A                                      CF06(06 'Adds record')
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A                                  9 20'Item Description'
     A                                      COLOR(WHT)
     A                                  9 53'Item Quantity'
     A                                      COLOR(WHT)
     A                                  9 69'Item Price'
     A                                      COLOR(WHT)
     A                                  9  2'Opt'
     A                                      COLOR(WHT)
     A                                  9  7'Item Number'
     A                                      COLOR(WHT)
     A                                  6  2'Type options, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  5'2=Edit      3=Copy      4=Delete  -
     A                                          5=Display'
     A                                      COLOR(BLU)
     A            S_POSTO        5S 0B  4 75
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A                                  4 52'Position to  . . . . .'
     A                                  5 52'Search for   . . .'
     A            S_SEARCH      10A  B  5 71
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A                                  1  2USER
     A            S_PGMNAM      10   O  2  2
     A                                  1 34'Programmers.io'
     A                                      COLOR(BLU)
     A                                  2 29'Item Maintenance Program'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A          R ITEMPTY
     A*%%TS  SD  20230712  002921  RAVISANKAR  REL-V7R4M0  5770-WDS
     A                                 11 28'---- No Records Found ----'
     A                                      DSPATR(BL)
     A          R ITFOOTER
     A*%%TS  SD  20230707  085741  RAVISANKAR  REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A                                 23  3'F3=Exit F6=Add F12=Cancel'
     A                                      COLOR(BLU)
     A            S_ERROR       50   O 23 30
